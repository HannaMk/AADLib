package pathfinder_hardware
public
with pathfinder_software;
with Devices::ADXL;
with buses::VME;
with buses::mil1553;
with memories;
with processors;
-----------------------------------------------------------------
-------------------------devices---------------------------------
-----------------------------------------------------------------
-- devices plugged on VME
device camera
	features
	camera_control	: in data port pathfinder_software::camera_control;
	images_data		: out data port pathfinder_software::images_data;
	req_access_vme	: requires bus access VME;

end camera;

device radio
	features
	emission_data	: in data port pathfinder_software::emission_data;
	reception_data	: out data port pathfinder_software::reception_data;
	req_access_vme	: requires bus access VME;

end radio;

device interface
	features
	req_access_vme 	: requires bus access VME;
	req_access_1553 : requires bus access bus_1553;
	control_data 	: in data port pathfinder_software::control_data;

	properties
	-- driver of the interface between VME and 1553
	device_driver => classifier (pathfinder_software::bus_interface);

end interface;

-- devices plugged on 1553

device rover_sojourner
-- rover Sojourner device
	features
	req_access_1553 : requires bus access bus_1553;
	properties
	-- driver of the communication between lander and sojourner
	device_driver => classifier (pathfinder_software::coupler);

end rover_sojourner;

device thrusters
	features
	control_data	: in data port pathfinder_software::control_data;
	req_access_1553	: requires bus access bus_1553;

end thrusters;

device valves
	features
	control_data	: in data port pathfinder_software::control_data;
	req_access_1553	: requires bus access bus_1553;

end valves;

device sun_sensors
	features
	measured_data	: out data port pathfinder_software::measured_data;
	req_access_1553	: requires bus access bus_1553;

end sun_sensors;

device star_analyser
	features
	measured_data	: out data port pathfinder_software::measured_data;
	req_access_1553	: requires bus access bus_1553;

end star_analyser;

device altimeter
features
	measured_data	: out data port pathfinder_software::measured_data;
	req_access_1553	: requires bus access bus_1553;
end altimeter;

device accelerometer extends Devices::ADXL::ADXL335
features
	measured_data	: out data port pathfinder_software::measured_data;
	req_access_1553	: requires bus access bus_1553;

end accelerometer;

device meteorological
features
	measured_data	: out data port pathfinder_software::measured_data;
	req_access_1553	: requires bus access bus_1553;

end meteorological;


------------------------------------------------------------------
-----------------------buses--------------------------------------
------------------------------------------------------------------
bus VME extends buses::VME::VME end VME;

bus bus_1553 extends buses::MIL1553::MIL1553 end bus_1553;
------------------------------------------------------------------
-----------------------memories-----------------------------------
------------------------------------------------------------------

memory memories extends memories::ROM
features
req_access_vme	: requires bus access  VME;
end memories;

-------------------------------------------------------------------
----------------------processor------------------------------------
-------------------------------------------------------------------
processor rs_6000 extends processors::rs_6000
	features
	pro_access_vme	: provides bus access pathfinder_hardware::VME;
	pro_access_1553	: provides bus access pathfinder_hardware::bus_1553;

end rs_6000;

processor implementation rs_6000.impl
	subcomponents
	memories			: memory memories;

	properties
	scheduling_protocol => (RMS);
	-- to analyse with
	-- scheduling_protocol => (EDF) ;

end rs_6000.impl;

end pathfinder_hardware;